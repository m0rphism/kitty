\begin{code}[hide]
module Paper.Section2-Intrinsic where

open import Data.List using (List; []; _∷_)

infix  4  _∋_  _⊢_
infixr 5  _⇒_  λx_
infixl 6  _·_
\end{code}

\newpage
\section{STLC with Intrinsic Typing}

\begin{code}
data Type : Set where
  ∅    : Type                -- Empty Type
  _⇒_  : Type → Type → Type  -- Function Type

variable
  t t₁ t₂  : Type
  Γ Γ₁ Γ₂  : List Type

-- Well-typed DeBruijn variables
data _∋_ : List Type → Type → Set where
  zero  : (t ∷ Γ ∋ t)
  suc   : (Γ ∋ t₁) → (t₂ ∷ Γ ∋ t₁)

-- Well-typed terms
data _⊢_ : List Type → Type → Set where
  `_   : (Γ ∋ t) → (Γ ⊢ t)                    -- Variable
  λx_  : (t₁ ∷ Γ ⊢ t₂) → (Γ ⊢ t₁ ⇒ t₂)        -- Function
  _·_  : (Γ ⊢ t₁ ⇒ t₂) → (Γ ⊢ t₁) → (Γ ⊢ t₂)  -- Application
\end{code}
%
\begin{code}[hide]
-- The application of the identity function
-- to itself is a well-typed term.
example₁ : [] ⊢ ∅ ⇒ ∅
example₁ = (λx (` zero)) · (λx (` zero))

private
  infix  4  _→ᵣ_  _→ₛ_
\end{code}
%
\begin{code}
  -- Parallel renamings and substitutions
  _→ᵣ_ _→ₛ_ : List Type → List Type → Set
  Γ₁ →ᵣ Γ₂ = ∀ t → (Γ₁ ∋ t) → (Γ₂ ∋ t)
  Γ₁ →ₛ Γ₂ = ∀ t → (Γ₁ ∋ t) → (Γ₂ ⊢ t)

  -- Lifting a renaming over a binder
  liftᵣ : (Γ₁ →ᵣ Γ₂) → (t ∷ Γ₁ →ᵣ t ∷ Γ₂)
  liftᵣ ρ _ zero     = zero
  liftᵣ ρ _ (suc x)  = suc (ρ _ x)

  -- Applying a renaming to a term
  rename : (Γ₁ →ᵣ Γ₂) → (Γ₁ ⊢ t) → (Γ₂ ⊢ t)
  rename ρ (` x)      = ` ρ _ x
  rename ρ (λx e)     = λx (rename (liftᵣ ρ) e)
  rename ρ (e₁ · e₂)  = rename ρ e₁ · rename ρ e₂

  -- Lifting a substitution over a binder
  liftₛ : (Γ₁ →ₛ Γ₂) → (t ∷ Γ₁ →ₛ t ∷ Γ₂)
  liftₛ σ _ zero     = ` zero
  liftₛ σ _ (suc x)  = rename (λ _ x → suc x) (σ _ x)

  -- Applying a substitution to a term
  subst : (Γ₁ →ₛ Γ₂) → (Γ₁ ⊢ t) → (Γ₂ ⊢ t)
  subst σ (` x)      = σ _ x
  subst σ (λx e)     = λx (subst (liftₛ σ) e)
  subst σ (e₁ · e₂)  = subst σ e₁ · subst σ e₂
\end{code}
\begin{code}[hide]
  infix 4 _↪_
  infixr 5 _∷ₛ_

  variable e e₁ e₂ e' e₁' e₂' : Γ ⊢ t

  -- Identity substitution
  idₛ : Γ →ₛ Γ
  idₛ = λ _ x → ` x

  -- Extending a substitution
  _∷ₛ_ : (Γ₂ ⊢ t) → (Γ₁ →ₛ Γ₂) → (t ∷ Γ₁ →ₛ Γ₂)
  (t ∷ₛ σ) _ zero     = t
  (t ∷ₛ σ) _ (suc x)  = σ _ x

  -- Small-step reduction
  data _↪_ : (Γ ⊢ t) → (Γ ⊢ t) → Set where
    β-λ·  : ((λx e₁) · e₂) ↪ subst (e₂ ∷ₛ idₛ) e₁
    ξ-λ   : (e ↪ e') → (λx e ↪ λx e')
    ξ-·₁  : (e₁ ↪ e₁') → (e₁ · e₂ ↪ e₁' · e₂)
    ξ-·₂  : (e₂ ↪ e₂') → (e₁ · e₂ ↪ e₁' · e₂)
\end{code}

-- This file defines a generic multi-sorted syntax and instantiates
-- the kit infrastructure for it.
--
-- The generic programming technique is closely oriented on the work
-- by Allais et al.[1] and at the end of the file an example is shown
-- of how to use it to model a simply typed lambda calculus.
--
-- The purpose of this file is to formally describe the syntaxes
-- supported by the reflection algorithm of our full framework.
--
-- In short: we support the same class of object languages as in the
-- work bai Allais et al.[1], but also polymorphism as we use the
-- intrinsic typing to describe the syntactic categories of our
-- untyped terms.
--
-- References:
-- [1] Guillaume Allais, Robert Atkey, James Chapman, Conor McBride,
--     and James McKinna. A type and scope safe universe of syntaxes
--     with binding: their semantics and proofs.
--     Proc. ACM Program. Lang., 2(ICFP):90:1--90:30, 2018.
--     doi:10.1145/3236785.

module Kitty.Examples.SystemF-Simple.Generics where

open import Data.List using (List; []; _âˆ·_; _++_)
open import Data.List.Relation.Unary.Any using (here; there)
open import Data.Product using (Î£; âˆƒ-syntax; Î£-syntax; _Ã—_; projâ‚; projâ‚‚; _,_)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; refl; sym; trans; cong; congâ‚‚; subst; module â‰¡-Reasoning)
open â‰¡-Reasoning

open import Kitty.Examples.SystemF-Simple.Kits

module WithSort (Sort : SortTy â†’ Set) where
  private variable
    st                        : SortTy
    s sâ‚ sâ‚‚ sâ‚ƒ s' sâ‚' sâ‚‚' sâ‚ƒ' : Sort st
    S Sâ‚ Sâ‚‚ Sâ‚ƒ S' Sâ‚' Sâ‚‚' Sâ‚ƒ' : List (Sort Var)

  ScopedT : Setâ‚
  ScopedT = âˆ€ {st} â†’ List (Sort Var) â†’ Sort st â†’ Set

  data Desc : Setâ‚ where
    `Ïƒ : (A : Set) â†’ (A â†’ Desc) â†’ Desc
    `X : âˆ€ {st} â†’ List (Sort Var) â†’ Sort st â†’ Desc â†’ Desc
    `â–  : âˆ€ {st} â†’ Sort st â†’ Desc

  âŸ¦_âŸ§ : Desc â†’ ScopedT â†’ ScopedT
  âŸ¦ `Ïƒ A d      âŸ§ X S s = Î£[ a âˆˆ A ] (âŸ¦ d a âŸ§ X S s)
  âŸ¦ `X S' s' d  âŸ§ X S s = X (S' ++ S) s' Ã— âŸ¦ d âŸ§ X S s
  âŸ¦ `â–  {st'} s' âŸ§ X {st} S s = Î£[ eq âˆˆ st' â‰¡ st ] s â‰¡ subst Sort eq s'

  data Tm (d : Desc) : ScopedT where
    `var : âˆ€ {S s} â†’ S âˆ‹ s â†’ Tm d S s
    `con : âˆ€ {st S} {s : Sort st} â†’ âŸ¦ d âŸ§ (Tm d) S s â†’ Tm d S s

  module WithDesc (d : Desc) where

    syn : Syntax
    syn = record
      { Sort        = Sort
      ; _âŠ¢_         = Tm d
      ; `_          = `var
      ; `-injective = Î» { refl â†’ refl }
      }

    open Syntax syn hiding (Sort; `_; `-injective) public

    mutual
      _â‹¯_ :
        âˆ€ {Sâ‚ : List (Sort Var)} {M : Sort st} {Sâ‚‚ : List (Sort Var)}
          {_âˆ‹/âŠ¢_ : Scoped} â¦ƒ ğ•‚ : Kit _âˆ‹/âŠ¢_ â¦„ â†’
        Tm d Sâ‚ M â†’ Sâ‚ â€“[ ğ•‚ ]â†’ Sâ‚‚ â†’ Tm d Sâ‚‚ M
      _â‹¯_ (`var x)  f = `/id (f _ x)
      _â‹¯_ (`con e') f = `con (e' â‹¯' f)

      _â‹¯'_ :
        âˆ€ {d'} {Sâ‚ : List (Sort Var)} {M : Sort st} {Sâ‚‚ : List (Sort Var)}
          {_âˆ‹/âŠ¢_ : Scoped} â¦ƒ ğ•‚ : Kit _âˆ‹/âŠ¢_ â¦„ â†’
        âŸ¦ d' âŸ§ (Tm d) Sâ‚ M â†’ Sâ‚ â€“[ ğ•‚ ]â†’ Sâ‚‚ â†’ âŸ¦ d' âŸ§ (Tm d) Sâ‚‚ M
      _â‹¯'_ {d' = `Ïƒ A d'}     (a , D') f = a , D' â‹¯' f
      _â‹¯'_ {d' = `X S' M' d'} (e , e') f = e â‹¯ (f â†‘* S') , e' â‹¯' f
      _â‹¯'_ {d' = `â–  M'}       e        f = e

    â‹¯-var :
      âˆ€ {Sâ‚ : List (Sort Var)} {m : Sort Var} {Sâ‚‚ : List (Sort Var)}
        {_âˆ‹/âŠ¢_ : Scoped} â¦ƒ ğ•‚ : Kit _âˆ‹/âŠ¢_ â¦„
        (x : Sâ‚ âˆ‹ m) (Ï• : Sâ‚ â€“[ ğ•‚ ]â†’ Sâ‚‚) â†’
      `/id (Ï• m x) â‰¡ `/id (Ï• m x)
    â‹¯-var x Ï• = refl

    mutual
      â‹¯-id :
        âˆ€ {_âˆ‹/âŠ¢_ : Scoped} â¦ƒ K : Kit _âˆ‹/âŠ¢_ â¦„
          {S st} {s : Sort st} (t : Tm d S s) â†’
        (t â‹¯ id) â‰¡ t
      â‹¯-id (`var x) = `/`-is-` x
      â‹¯-id (`con e) = cong `con (â‹¯-id' e)

      â‹¯-id' :
        âˆ€ {d'} {_âˆ‹/âŠ¢_ : Scoped} â¦ƒ K : Kit _âˆ‹/âŠ¢_ â¦„
          {S st} {s : Sort st} (t : âŸ¦ d' âŸ§ (Tm d) S s) â†’
        (t â‹¯' id) â‰¡ t
      â‹¯-id' {d' = `Ïƒ A d'}     (a , D')  = cong (a ,_) (â‹¯-id' D')
      â‹¯-id' {d' = `X S' M' d'} (eâ‚ , eâ‚‚) = congâ‚‚ _,_ (trans (cong (eâ‚ â‹¯_) (~-ext (idâ†‘*~id S'))) (â‹¯-id eâ‚)) (â‹¯-id' eâ‚‚)
      â‹¯-id' {d' = `â–  M'}       (refl , refl) = refl

    KT : Traversal
    KT = record { _â‹¯_ = _â‹¯_ ; â‹¯-var = â‹¯-var ; â‹¯-id  = â‹¯-id }

    open Traversal KT hiding (_â‹¯_; â‹¯-id; â‹¯-var) public

    mutual
      fusion :
        âˆ€ {_âˆ‹/âŠ¢â‚_ _âˆ‹/âŠ¢â‚‚_ _âˆ‹/âŠ¢_ : Scoped}
          â¦ƒ Kâ‚ : Kit _âˆ‹/âŠ¢â‚_ â¦„ â¦ƒ Kâ‚‚ : Kit _âˆ‹/âŠ¢â‚‚_ â¦„ â¦ƒ K : Kit _âˆ‹/âŠ¢_ â¦„
          â¦ƒ Wâ‚ : WkKit Kâ‚ â¦„ â¦ƒ C : CKit Kâ‚ Kâ‚‚ K â¦„
          (t : Sâ‚ âŠ¢ s) (Ï•â‚ : Sâ‚ â€“[ Kâ‚ ]â†’ Sâ‚‚) (Ï•â‚‚ : Sâ‚‚ â€“[ Kâ‚‚ ]â†’ Sâ‚ƒ)
        â†’ (t â‹¯ Ï•â‚) â‹¯ Ï•â‚‚ â‰¡ t â‹¯ (Ï•â‚ Â·â‚– Ï•â‚‚)
      fusion (`var x)  Ï•â‚ Ï•â‚‚ = sym (&/â‹¯-â‹¯ (Ï•â‚ _ x) Ï•â‚‚)
      fusion (`con e') Ï•â‚ Ï•â‚‚ = cong `con (fusion' e' Ï•â‚ Ï•â‚‚)

      fusion' :
        âˆ€ {d'} {_âˆ‹/âŠ¢â‚_ _âˆ‹/âŠ¢â‚‚_ _âˆ‹/âŠ¢_ : Scoped}
          â¦ƒ Kâ‚ : Kit _âˆ‹/âŠ¢â‚_ â¦„ â¦ƒ Kâ‚‚ : Kit _âˆ‹/âŠ¢â‚‚_ â¦„ â¦ƒ K : Kit _âˆ‹/âŠ¢_ â¦„
          â¦ƒ Wâ‚ : WkKit Kâ‚ â¦„ â¦ƒ C : CKit Kâ‚ Kâ‚‚ K â¦„
          (t : âŸ¦ d' âŸ§ (Tm d) Sâ‚ s) (Ï•â‚ : Sâ‚ â€“[ Kâ‚ ]â†’ Sâ‚‚) (Ï•â‚‚ : Sâ‚‚ â€“[ Kâ‚‚ ]â†’ Sâ‚ƒ)
        â†’ (t â‹¯' Ï•â‚) â‹¯' Ï•â‚‚ â‰¡ t â‹¯' (Ï•â‚ Â·â‚– Ï•â‚‚)
      fusion' {d' = `Ïƒ A d'}     (a , D')  Ï•â‚ Ï•â‚‚ = cong (a ,_) (fusion' D' Ï•â‚ Ï•â‚‚)
      fusion' {d' = `X S' M' d'} (eâ‚ , eâ‚‚) Ï•â‚ Ï•â‚‚ = congâ‚‚ _,_ (trans (fusion eâ‚ (Ï•â‚ â†‘* S') (Ï•â‚‚ â†‘* S'))
                                                                    (cong (eâ‚ â‹¯_) (sym (~-ext (dist-â†‘*-Â· S' Ï•â‚ Ï•â‚‚)))) )
                                                              (fusion' eâ‚‚ Ï•â‚ Ï•â‚‚)
      fusion' {d' = `â–  M'}       (refl , refl) Ï•â‚ Ï•â‚‚ = refl

    CT : CTraversal
    CT = record { fusion = fusion }

    open CTraversal CT public hiding (fusion)

module Example-STLC where
  data Sort : SortTy â†’ Set where
    ğ•– : Sort Var

  open WithSort Sort

  data Label : Set where
    [Î»] [Â·] : Label

  desc : Desc
  desc = `Ïƒ Label Î» where
    [Î»] â†’ `X (ğ•– âˆ· []) ğ•– (`â–  ğ•–)
    [Â·] â†’ `X [] ğ•– (`X [] ğ•– (`â–  ğ•–))

  open WithDesc desc

  pattern `Î»_ e     = `con ([Î»] , e , (refl , refl))
  pattern _Â·_ eâ‚ eâ‚‚ = `con ([Â·] , eâ‚ , eâ‚‚ , (refl , refl))
  pattern `_ x      = `var x

  `id : [] âŠ¢ ğ•–
  `id = `Î» (` zero)

  test : (` zero) â‹¯ â¦… `id â¦† â‰¡ `id
  test = refl

  
